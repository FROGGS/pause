#!/usr/local/bin/perl -w -- -*- mode: cperl -*-

use lib "/home/k/PAUSE/lib";
use PAUSE ();

use strict;
use vars qw($Id);

$Id = q$Id: rm_stale_links 385 2003-07-25 19:50:38Z k $;

use File::Find;
chdir "$PAUSE::Config->{MLROOT}../..";

find(
     {
      bydepth => 1,
      wanted => sub {
        return if /^\.\.?$/;
        my($dev,$ino,$mode,$nlink,$uid,$gid) = lstat($_);
        if (-l $_ && ! -e $_){
          warn "unlinking stale $File::Find::name\n";
          unlink $_;
          PAUSE::delfile_hook($_);
        }
        if ($_ eq "CHECKSUMS") {
          opendir DIR, ".";
          my @readdir = readdir DIR;
          closedir DIR;
          if (@readdir == 3) {
            warn "unlinking orphaned $File::Find::name\n";
            unlink $_ or die "Could not unlink $_: $!";
            PAUSE::delfile_hook($_);
          }
        }
        return if -l $_;
        ($dev,$ino,$mode,$nlink,$uid,$gid) = stat($_);
        if (-d _ and $nlink == 2) { # directory without subdir
          opendir DIR, $_;
          my @readdir = readdir DIR;
          closedir DIR;
          if (@readdir == 2) {
            warn "rmdirring empty $File::Find::name\n";
            rmdir $_ or die "Could not rmdir $_: $!";
            PAUSE::delfile_hook($_);
          }
        }
      },
     },
     "authors", "modules");
